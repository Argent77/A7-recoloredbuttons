BACKUP ~weidu_external/backup/recoloredbuttons~
SUPPORT ~http://www.shsforums.net/topic/57057-mod-recolored-toolbar-buttons-for-bgee-bg2ee/~
VERSION ~v4.2~

ALWAYS
  // required because of non-standard BACKUP path
  OUTER_TEXT_SPRINT MOD_FOLDER ~recoloredbuttons~
  // root folder for mutable data
  OUTER_TEXT_SPRINT WORK_FOLDER ~weidu_external~
END

README ~recoloredbuttons/readme/readme-%LANGUAGE%.txt~
       ~recoloredbuttons/readme/readme.txt~

LANGUAGE  ~English~
          ~english~
          ~recoloredbuttons/languages/english/setup.tra~

LANGUAGE  ~Polski (tlumaczenie: Cahir)~
          ~polish~
          ~recoloredbuttons/languages/english/setup.tra~
          ~recoloredbuttons/languages/polish/setup.tra~

LANGUAGE  ~Deutsch~
          ~german~
          ~recoloredbuttons/languages/english/setup.tra~
          ~recoloredbuttons/languages/german/setup.tra~


BEGIN @3    // Bluish color scheme
  REQUIRE_PREDICATE GAME_IS ~bgee~ @100   // This mod requires Baldur's Gate: Enhanced Edition.
  SUBCOMPONENT @1   // Recolored character's toolbar buttons (BG:EE)
  DESIGNATED 0
  LABEL ~A7-RECOLOREDBUTTONS-BGEE-BLUISH~

  COPY ~recoloredbuttons/bgee/GUIBTACT_blue.BAM~ ~override/GUIBTACT.BAM~
    LPF ~UPDATE_PVRZ_INDICES~ RET original_base_index new_base_index END

  ACTION_IF (original_base_index >= 0 && new_base_index >= 0) BEGIN
    ACTION_FOR_EACH file IN ~MOS1000.PVRZ~ BEGIN
      LAF ~INSTALL_PVRZ~ INT_VAR original_base_index new_base_index STR_VAR source_file = EVAL ~recoloredbuttons/bgee/%file%~ END
    END
  END


BEGIN @4    // Golden color scheme
  REQUIRE_PREDICATE GAME_IS ~bgee~ @100   // This mod requires Baldur's Gate: Enhanced Edition.
  SUBCOMPONENT @1   // Recolored character's toolbar buttons (BG:EE)
  DESIGNATED 1
  LABEL ~A7-RECOLOREDBUTTONS-BGEE-GOLDEN~

  COPY ~recoloredbuttons/bgee/GUIBTACT_gold.BAM~ ~override/GUIBTACT.BAM~
    LPF ~UPDATE_PVRZ_INDICES~ RET original_base_index new_base_index END

  ACTION_IF (original_base_index >= 0 && new_base_index >= 0) BEGIN
    ACTION_FOR_EACH file IN ~MOS1001.PVRZ~ BEGIN
      LAF ~INSTALL_PVRZ~ INT_VAR original_base_index new_base_index STR_VAR source_file = EVAL ~recoloredbuttons/bgee/%file%~ END
    END
  END


BEGIN @2    // Recolored character's toolbar buttons (BG2:EE)
  REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ @101    // This mod requires Baldur's Gate II: Enhanced Edition.
  DESIGNATED 2
  LABEL ~A7-RECOLOREDBUTTONS-BG2EE~

  COPY ~recoloredbuttons/bg2ee/GUIBTACT.BAM~ ~override~
    LPF ~UPDATE_PVRZ_INDICES~ RET original_base_index new_base_index END

  ACTION_IF (original_base_index >= 0 && new_base_index >= 0) BEGIN
    ACTION_FOR_EACH file IN ~MOS1000.PVRZ~ BEGIN
      LAF ~INSTALL_PVRZ~ INT_VAR original_base_index new_base_index STR_VAR source_file = EVAL ~recoloredbuttons/bg2ee/%file%~ END
    END
  END
